import awt.Color as ImageColor;
import awt.image.BufferedImage;
import imageio.ImageIO;

class Vec { 
   const x;
   const y;
   const z;
   new(x, y, z) {
     this.x = x;
     this.y = y;
     this.z = z;
   }
   static times(k, v) { 
      return new Vec(k * v.x, k * v.y, k * v.z); 
   }
   static minus(v1, v2) { 
      return new Vec(v1.x - v2.x, v1.y - v2.y, v1.z - v2.z); 
   }
   static plus(v1, v2) { 
      return new Vec(v1.x + v2.x, v1.y + v2.y, v1.z + v2.z); 
   }
   static dot(v1, v2) { 
      return v1.x * v2.x + v1.y * v2.y + v1.z * v2.z; 
   }
   static mag(v) { 
      return Math.sqrt(v.x * v.x + v.y * v.y + v.z * v.z); 
   }
   static norm(v) {
      let mag = Vec.mag(v);
      let div = (mag == 0) ? Double.POSITIVE_INFINITY : 1.0 / mag;
      return Vec.times(div, v);
   }
   static cross(v1, v2) {
      return new Vec(v1.y * v2.z - v1.z * v2.y,
                    v1.z * v2.x - v1.x * v2.z,
                    v1.x * v2.y - v1.y * v2.x);
   }
}

class Color {
   const r;
   const g;
   const b;
   new(r, g, b) {
     this.r = r;
     this.g = g;
     this.b = b;
   }
   static scale(k, v) { 
      return new Color(k * v.r, k * v.g, k * v.b); 
   }
   static plus(v1, v2) { 
      return new Color(v1.r + v2.r, v1.g + v2.g, v1.b + v2.b); 
   }
   static times(v1, v2) { 
      return new Color(v1.r * v2.r, v1.g * v2.g, v1.b * v2.b); 
   }
   static const white = new Color(1.0, 1.0, 1.0);
   static const grey = new Color(0.5, 0.5, 0.5);
   static const black = new Color(0.0, 0.0, 0.0);
   static const background = new Color(0.0, 0.0, 0.0);
   static const defaultColor = new Color(0.0, 0.0, 0.0);
   static toDrawingColor(c) {
      let legalize = (d) -> d > 1 ? 1 : d;
      return new ImageColor(
         Math.round(legalize(c.r) * 255),
         Math.round(legalize(c.g) * 255),
         Math.round(legalize(c.b) * 255)
      );
   }
}

class Camera {
   let forward;
   let right;
   let up;
   let pos;
   
   new(pos, lookAt) {
      let down = new Vec(0.0, -1.0, 0.0);
      this.pos = pos;
      this.forward = Vec.norm(Vec.minus(lookAt, pos));
      this.right = Vec.times(1.5, Vec.norm(Vec.cross(forward, down)));
      this.up = Vec.times(1.5, Vec.norm(Vec.cross(forward, right)));
   }
}

class Ray {
   let start;
   let dir;
   new(start, dir){
     this.start = start;
     this.dir = dir;
   }
}

class Intersection {
   let thing;
   let ray;
   let dist;
   new(thing, ray, dist){
     this.thing = thing;
     this.ray = ray;
     this.dist = dist;
   }
}

abstract class Surface {
   let roughness;
   abstract diffuse(pos);
   abstract specular(pos);
   abstract reflect(pos);
}

abstract class Thing {
   let surface: Surface;
   abstract intersect(ray);
   abstract normal(pos);
}

class Light {
   let pos;
   let color;
   new(pos, color){
     this.pos = pos;
     this.color = color;
   }
}

class Scene {
   let things;//[];
   let lights;//: Light[];
   let camera: Camera;
   new(things, lights, camera: Camera) {
     this.things = things;
     this.lights = lights;
     this.camera = camera;
   }
}

class Sphere extends Thing {
   let radius2;
   let center;
   new(center, radius, surface: Surface) {
      this.radius2 = radius * radius;
      this.center = center;
      this.surface = surface;
   }
   override normal(pos) { return Vec.norm(Vec.minus(pos, center)); }
   override intersect(ray) {
      let eo = Vec.minus(center, ray.start);
      let v = Vec.dot(eo, ray.dir);
      let dist = 0;
      if (v >= 0) {
         let disc = radius2 - (Vec.dot(eo, eo) - v * v);
         if (disc >= 0) {
            dist = v - Math.sqrt(disc);
         }
      }
      if (dist == 0) {
         return null;
      } else {
         return new Intersection(this, ray, dist);
      }
   }
}

class Plane extends Thing {
   let norm;
   let offset;
   new(norm, offset, surface: Surface) {
      this.norm = norm;
      this.offset = offset;
      this.surface = surface;
   }
   override normal(pos) { return norm; }
   override intersect(ray) {
     let denom = Vec.dot(norm, ray.dir);
     if (denom > 0) {
        return null;
     } else {
        let dist = (Vec.dot(norm, ray.start) + offset) / (-denom);
        return new Intersection(this, ray, dist);
     }
   }

}

class ShinySurface extends Surface {
   new() {
     this.roughness = 250;
   }
   override diffuse(pos) { return Color.white; }
   override specular(pos) { return Color.grey; }
   override reflect(pos) { return 0.7; }
}

class CheckerboardSurface extends Surface {
   new() {
     this.roughness = 150;
   }
   override diffuse(pos) {
     if ((Math.floor(pos.z) + Math.floor(pos.x)) % 2 != 0) {
        return Color.white;
     } else {
        return Color.black;
     }
   }
   override specular(pos) { return Color.white; }
   override reflect(pos) {
     if ((Math.floor(pos.z) + Math.floor(pos.x)) % 2 != 0) {
       return 0.1;
     } else {
       return 0.7;
     }
   }
}


class RayTracer {
   let maxDepth = 5;

   private intersections(ray, scene) {
      let closest = Double.POSITIVE_INFINITY;
      let closestInter = null;
      for (let thing in scene.things) {
         let inter = thing.intersect(ray);
         if (inter != null && inter.dist < closest) {
            closestInter = inter;
            closest = inter.dist;
         }
      }
      return closestInter;
   }

   private testRay(ray, scene) {
      let isect = this.intersections(ray, scene);
      if (isect != null) {
         return isect.dist;
      } else {
         return null;
      }
   }

   private traceRay(ray, scene, depth) {
      let isect = this.intersections(ray, scene);
      if (isect == null) {
         return Color.background;
      } else {
         return this.shade(isect, scene, depth);
      }
   }

   private shade(isect, scene, depth) {
      let d = isect.ray.dir;
      let pos = Vec.plus(Vec.times(isect.dist, d), isect.ray.start);
      let normal = isect.thing.normal(pos);
      let reflectDir = Vec.minus(d, Vec.times(2, Vec.times(Vec.dot(normal, d), normal)));
      let naturalColor = Color.plus(Color.background,
                             this.getNaturalColor(isect.thing, pos, normal, reflectDir, scene));
      let reflectedColor = (depth >= this.maxDepth) ? Color.grey : this.getReflectionColor(isect.thing, pos, normal, reflectDir, scene, depth);
      return Color.plus(naturalColor, reflectedColor);
   }

   private getReflectionColor(thing, pos, normal, rd, scene, depth) {
      return Color.scale(thing.surface.reflect(pos), this.traceRay(new Ray(pos, rd), scene, depth + 1));
   }

   private getNaturalColor(thing, pos, norm, rd, scene) {
      let addLight = (col, light) -> {
         let ldis = Vec.minus(light.pos, pos);
         let livec = Vec.norm(ldis);
         let neatIsect = this.testRay(new Ray(pos, livec), scene);
         let isInShadow = (neatIsect == null) ? false : (neatIsect <= Vec.mag(ldis));
         if (isInShadow) {
            return col;
         } else {
            let illum = Vec.dot(livec, norm);
            let lcolor = (illum > 0) ? Color.scale(illum, light.color)
                                : Color.defaultColor;
            let specular = Vec.dot(livec, Vec.norm(rd));
            let scolor = (specular > 0) ? Color.scale(Math.pow(specular, thing.surface.roughness), light.color)
                                : Color.defaultColor;
            return Color.plus(col, Color.plus(Color.times(thing.surface.diffuse(pos), lcolor),
                                      Color.times(thing.surface.specular(pos), scolor)));
         }
      };
      return reduce(scene.lights, addLight, Color.defaultColor);
   }
   
   reduce(lights, addLight, color) { // scene.lights.reduce
     let result = color;
     for(let light in lights){
       result = addLight(result, light);
     }
     return result;
   }

   render(scene, graphics, screenWidth, screenHeight) {
      let getPoint = (x, y, camera) -> {
         let recenterX = x ->(x - (screenWidth / 2.0)) / 2.0 / screenWidth;
         let recenterY = y -> -1 * (y - (screenHeight / 2.0)) / 2.0 / screenHeight;
         return Vec.norm(Vec.plus(camera.forward, Vec.plus(Vec.times(recenterX(x), camera.right), Vec.times(recenterY(y), camera.up))));
      };
      //for (let y = 0; y < screenHeight; y++) {
      //   for (let x = 0; x < screenWidth; x++) {
      for(let y in 0 .. screenHeight -1){
         for(let x in 0 .. screenWidth -1){
            let color = this.traceRay(new Ray(scene.camera.pos, getPoint(x, y, scene.camera)), scene, 0);
            let rgb = Color.toDrawingColor(color);
            
            graphics.setColor(rgb);
            graphics.fillRect(x, y, x + 1, y + 1);
         }
      }
   }
}


func defaultScene(){
   return new Scene(
            [new Plane(new Vec(0.0, 1.0, 0.0), 0.0, new CheckerboardSurface()),
             new Sphere(new Vec(0.0, 1.0, -0.25), 1.0, new ShinySurface()),
             new Sphere(new Vec(-1.0, 0.5, 1.5), 0.5, new ShinySurface())],
            [new Light(new Vec(-2.0, 2.5, 0.0), new Color(0.49, 0.07, 0.07)),
             new Light(new Vec(1.5, 2.5, 1.5), new Color(0.07, 0.07, 0.49)),
             new Light(new Vec(1.5, 2.5, -1.5), new Color(0.07, 0.49, 0.071)),
             new Light(new Vec(0.0, 3.5, 0.0), new Color(0.21, 0.21, 0.35))],
            new Camera(new Vec(3.0, 2.0, 4.0), new Vec(-1.0, 0.5, 0.0))
   );
}
// time for debug: 46931, 46477
// time for run: 41837, 42174, 39594, 39501, 40236
// time for new run: 24101, 23933
func start(path) {
   let rayTracer = new RayTracer();
   let image = new BufferedImage(256, 256, BufferedImage.TYPE_INT_ARGB);
   let file = new File(path);
   let graphics = image.getGraphics();
   let scene = defaultScene();
   
   rayTracer.render(scene, graphics, 256, 256);
   ImageIO.write(image, "png", file);   
}

let start = System.currentTimeMillis();
start("c:\\Work\\temp\\raytrace.png");
println(System.currentTimeMillis()-start);
